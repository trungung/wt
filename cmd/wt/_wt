#compdef wt

local context state state_descr line
typeset -A opt_args

_arguments -C \
    '(-f --from)'{-f,--from}'[base branch to create from]:branch:_wt_all_branches' \
    '1: :->command_or_branch' \
    '*:: :->args'

case $state in
    command_or_branch)
        local -a subcommands
        subcommands=(
            'init:create .wt.config.json'
            'exec:run an arbitrary command inside a branchâ€™s worktree'
            'remove:remove a worktree and optionally delete its branch'
            'prune:remove worktrees whose branches are merged into default branch'
            'health:check project health'
        )
        _describe -t subcommands 'subcommands' subcommands
        _wt_existing_branches
        ;;
    args)
        case $line[1] in
            remove)
                _arguments \
                    '(-r --force)'{-r,--force}'[force removal even if dirty]' \
                    '1:branch:_wt_existing_branches'
                ;;
            prune)
                _arguments \
                    '(-f --force)'{-f,--force}'[force removal even if dirty]' \
                    '--dry-run[show what would be removed]' \
                    '--fetch[run git fetch --prune first]'
                ;;
            init)
                _arguments \
                    '(-y --yes)'{-y,--yes}'[write defaults without prompts]'
                ;;
            exec)
                _arguments \
                    '1:branch:_wt_existing_branches' \
                    '(--)--[delimiter]' \
                    '*::command:_normal'
                ;;
        esac
        ;;
esac

_wt_existing_branches() {
    local -a branches
    # Get branches from git worktree list
    branches=(${(f)"$(git worktree list --porcelain 2>/dev/null | grep '^branch ' | sed 's|^branch refs/heads/||')"})
    if (( $#branches > 0 )); then
        _describe -t branches 'existing worktree branches' branches
    fi
}

_wt_all_branches() {
    local -a branches
    branches=(${(f)"$(git branch --format='%(refname:short)' 2>/dev/null)"})
    if (( $#branches > 0 )); then
        _describe -t branches 'branches' branches
    fi
}
